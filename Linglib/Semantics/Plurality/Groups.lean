import Mathlib.Data.Finset.Grade
import Mathlib.Logic.Equiv.Finset
import Linglib.Semantics.Mereology

/-!
# Group formation

This file defines Landman's group operators on a join-semilattice `E` of individuals or events.
Formation `up` packs a plural sum into an atom, the group, and dissolution `down` recovers the
sum ([landman-1989]; [landman-2000]). The carrier is any `SemilatticeSup`, so the operators
serve the domain of individuals and the domain of events alike; in the event domain, a
symmetric verb's atomic event dissolves into the sum of its directional sub-events
([siloni-2012]). A model on finite sets shows the two laws consistent.

## Definitions

* `Plurality.GroupStructure E`: `up` and `down`, with a group an atom and dissolution the
  inverse of formation.
* `Plurality.GroupStructure.finsetModel β`: the model on `Finset (β ⊕ ℕ)`, packing a plurality
  into the singleton of a fresh marker indexed by its code.

## Main results

* `Plurality.GroupStructure.up_injective`: distinct sums form distinct groups.

## References

* [F. Landman, *Groups, I* (1989)][landman-1989]
* [F. Landman, *Events and plurality* (2000)][landman-2000]
* [T. Siloni, *Reciprocal verbs and symmetry* (2012)][siloni-2012]
-/

namespace Plurality

/-- Landman's group structure: `up` packs a sum into a group atom and `down` recovers the sum. -/
structure GroupStructure (E : Type*) [SemilatticeSup E] where
  /-- Group formation. -/
  up : E → E
  /-- Group dissolution. -/
  down : E → E
  /-- A group is an atom. -/
  atom_up (x : E) : Mereology.Atom (up x)
  /-- Dissolution inverts formation. -/
  down_up (x : E) : down (up x) = x

namespace GroupStructure

variable {E : Type*} [SemilatticeSup E] (G : GroupStructure E)

theorem up_injective : Function.Injective G.up :=
  Function.LeftInverse.injective G.down_up

/-! ### A model

Finite subsets of `β ⊕ ℕ` under union: the `β`-singletons are the ordinary atoms, and packing
recruits the singleton of a fresh `ℕ`-marker for each plurality, so groups are atoms of the same
domain. -/

variable {β : Type*} [Encodable β]

/-- Group formation in the model: the singleton of the marker indexed by the plurality's
code. -/
def modelUp (x : Finset (β ⊕ ℕ)) : Finset (β ⊕ ℕ) :=
  {Sum.inr (Encodable.encode x)}

theorem modelUp_injective : Function.Injective (modelUp (β := β)) := λ _ _ h =>
  Encodable.encode_injective (Sum.inr.inj (Finset.singleton_injective h))

/-- The finite sets of `β ⊕ ℕ` carry a group structure, with dissolution the left inverse of
formation. -/
noncomputable def finsetModel (β : Type*) [DecidableEq β] [Encodable β] :
    GroupStructure (Finset (β ⊕ ℕ)) where
  up := modelUp
  down := Function.invFun modelUp
  atom_up _ := Mereology.atom_iff_isAtom.2 (Finset.isAtom_singleton _)
  down_up := Function.leftInverse_invFun modelUp_injective

@[simp] theorem finsetModel_up [DecidableEq β] (x : Finset (β ⊕ ℕ)) :
    (finsetModel β).up x = modelUp x := rfl

end GroupStructure

end Plurality
